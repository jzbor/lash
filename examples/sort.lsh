@set strategy normal;
@usestd;


one := !n (SUCC NIL);
two := !n (SUCC one);
three := !n (SUCC two);
four := !n (SUCC three);
iszero := λn.n (λx. FALSE) TRUE;


le' := \f x y. IFTHENELSE
		(OR (iszero x) (iszero y))
		(iszero x)
		(f f (PRED x) (PRED y));
le := le' le';


insert' := \f e l. IFTHENELSE
		(ISNIL l)
		(CONS e l)
		(IFTHENELSE
			(le e (HEAD l))
			(CONS e l)
			(CONS (HEAD l) (f f e (TAIL l)))
		);
insert := insert' insert';


sort' := \f l. IFTHENELSE
		(ISNIL l)
		NIL
		(insert (HEAD l) (f f (TAIL l)));
sort := sort' sort';


@echo "";
@echo "Unsorted list:";
listunsorted := !debug (!norm (CONS three (CONS one (CONS four ((CONS two NIL))))));

@echo "";
@echo "After sorting:";
!debug (!time (!cnorm (sort listunsorted)));

